/*
 * Copyright 2013-2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */







buildscript {
    repositories {
        maven { url 'http://repo.springsource.org/plugins-release' }
    }
    dependencies {
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:0.7.2")
    }
}

apply from: "gradle/environment.gradle"
apply from: "gradle/version.gradle"
apply from: "gradle/checkstyle.gradle"
apply from: "gradle/java.gradle"
apply from: "gradle/javadoc.gradle"
apply from: "gradle/maven.gradle"

allprojects {
    group = 'org.springframework.cloud.aws'
    apply plugin: "project-report"
}

subprojects {

    apply plugin: 'jdepend'

    // Custom properties
    ext {
        springFrameworkVersion = "4.1.1.RELEASE"
        springRetryVersion = "1.1.0.RELEASE"
        junitVersion = '4.11'
        slf4jVersion = '1.7.2'
        log4jVersion = '1.2.17'
        awsSdkVersion = '1.8.2'
        mockitoVersion = '1.9.5'
        hamcrestVersion = '1.3'
        tomcatVersion = "7.0.34"
    }

    repositories {
        maven { url 'http://repo.spring.io/snapshot' }
    }

    //Default dependencies
    dependencies {
        compile("org.slf4j:slf4j-api:${slf4jVersion}")
        testRuntime("org.slf4j:slf4j-jcl:${slf4jVersion}")
        testRuntime("log4j:log4j:${log4jVersion}")
        testCompile("junit:junit:${junitVersion}")
        testCompile("org.mockito:mockito-core:${mockitoVersion}")
        testCompile("org.hamcrest:hamcrest-core:${hamcrestVersion}")
    }


    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    test {
        systemProperty("java.awt.headless", "true")
    }
}

project('spring-cloud-aws-core') {
    description = 'spring cloud aws core'
    dependencies {
        compile("org.springframework:spring-core:${springFrameworkVersion}")
        compile("com.amazonaws:aws-java-sdk:${awsSdkVersion}") {
            exclude module: "stax-api"
            exclude module: "activation"
        }
        compile("org.springframework:spring-beans:${springFrameworkVersion}")
        testCompile("org.springframework:spring-test:${springFrameworkVersion}")
        testCompile("org.springframework:spring-context:${springFrameworkVersion}")
    }
}


project('spring-cloud-aws-context') {
    description = "spring cloud aws context"

    def memcachedClientVersion = "1.0.1"

    configurations {
        jarjar
        memCachedClient
    }

    task memCachedClientRepackJar(type: Jar) { repackJar ->
        repackJar.baseName = "spring-cloud-aws-memcached-repack"
        repackJar.version = memcachedClientVersion

        doLast() {
            project.ant {
                taskdef name: "jarjar", classname: "com.tonicsystems.jarjar.JarJarTask", classpath: configurations.jarjar.asPath
                jarjar(destfile: repackJar.archivePath) {
                    zipfileset(src: "${rootDir}/lib/AmazonElastiCacheClusterClient-1.0.1.jar")
                    rule(pattern: "net.spy.memcached.**", result: "org.springframework.cloud.aws.cache.memcached.@1")
                }
            }
        }
    }

    tasks.compileJava.dependsOn(memCachedClientRepackJar)

    dependencies {
        compile project(":spring-cloud-aws-core")
        compile("org.springframework:spring-context:${springFrameworkVersion}")
        compile("org.springframework:spring-context-support:${springFrameworkVersion}")
        compile("javax.mail:mailapi:1.4.3") {
            exclude module: "activation"
        }
        compile files(memCachedClientRepackJar.archivePath)

        testCompile("org.springframework:spring-test:${springFrameworkVersion}")
        testCompile("com.thimbleware.jmemcached:jmemcached-core:1.0.0")

        jarjar("com.googlecode.jarjar:jarjar:1.3")
    }

    jar {
        dependsOn memCachedClientRepackJar
        from(zipTree(memCachedClientRepackJar.archivePath)) {
            include "org/elasticspring/cache/memcached/**"
        }
    }
}

project('spring-cloud-aws-jdbc') {
    description = 'spring cloud aws jdbc'
    dependencies {
        compile project(":spring-cloud-aws-context")
        compile("org.springframework:spring-jdbc:${springFrameworkVersion}")

        compile("org.apache.tomcat:tomcat-jdbc:${tomcatVersion}") {
            exclude module: "tomcat-juli"
        }

        compile("org.springframework.retry:spring-retry:${springRetryVersion}") {
            exclude module: "spring-context"
        }

        testRuntime("mysql:mysql-connector-java:5.1.16")
        testRuntime("org.apache.tomcat.extras:tomcat-extras-juli-adapters:${tomcatVersion}")
        testCompile("org.springframework:spring-test:${springFrameworkVersion}")
    }
}


project('spring-cloud-aws-messaging') {
    description = 'spring cloud aws messaging'
    dependencies {
        compile project(":spring-cloud-aws-context")
        compile("org.springframework:spring-messaging:${springFrameworkVersion}")
        compile("javax.servlet:javax.servlet-api:3.0.1")
        compile("org.springframework:spring-web:${springFrameworkVersion}")
        testCompile("org.springframework:spring-test:${springFrameworkVersion}")
        testCompile("org.springframework:spring-webmvc:${springFrameworkVersion}")
    }
}

project('spring-cloud-aws-integration-test') {
    description = 'spring cloud aws integration test on AWS'
    dependencies {
        testCompile project(":spring-cloud-aws-core")
        testCompile project(":spring-cloud-aws-context")
        testCompile project(":spring-cloud-aws-jdbc")
        testCompile project(":spring-cloud-aws-messaging")
        testCompile("org.springframework:spring-test:${springFrameworkVersion}")

        testRuntime("org.apache.tomcat:tomcat-jdbc:${tomcatVersion}") {
            exclude module: "tomcat-juli"
        }

        testRuntime("mysql:mysql-connector-java:5.1.16")
        testRuntime("org.apache.tomcat.extras:tomcat-extras-juli-adapters:${tomcatVersion}")
        testRuntime("javax.mail:mailapi:1.4.3") {
            exclude module: "activation"
        }
        testRuntime("org.springframework.retry:spring-retry:${springRetryVersion}") {
            exclude module: "spring-context"
        }
        testCompile("org.springframework:spring-context-support:${springFrameworkVersion}")
        testRuntime("org.aspectj:aspectjweaver:1.6.8")

        testRuntime("javax.servlet:javax.servlet-api:3.0.1")
        testCompile("org.springframework:spring-web:${springFrameworkVersion}")
    }

    test.onlyIf { System.getProperty("els.config.dir") != null }
    test.systemProperty("els.config.dir", System.getProperty("els.config.dir"))
    configurations.archives.artifacts.clear()
}



description = 'Spring Cloud AWS'

apply plugin: 'org.asciidoctor.gradle.asciidoctor'

// don't publish the default jar for the root project
configurations.archives.artifacts.clear()

task api(type: Javadoc) {
    group = "Documentation"
    description = "Generates aggregated Javadoc API documentation."
    title = "${rootProject.description} ${version} API"

    dependsOn {
        subprojects.collect {
            it.tasks.getByName("jar")
        }
    }
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.author = true
    options.header = rootProject.description
    options.splitIndex = true
    options.links(project.ext.javadocLinks)

    source subprojects.collect { project ->
        project.sourceSets.main.allJava
    }

    classpath = files(subprojects.collect { project ->
        project.sourceSets.main.compileClasspath
    })

    maxMemory = "1024m"
    destinationDir = new File(buildDir as String, "api")
}


task docsZip(type: Zip) {
    group = "Distribution"
    baseName = "spring-cloud-aws"
    classifier = "docs"
    description = "Builds -${classifier} archive containing api and reference "

    from(api) {
        into "javadoc-api"
    }

    from(asciidoctor) {
        into "spring-cloud-aws-reference"
    }
}

task schemaZip(type: Zip) {
    group = "Distribution"
    baseName = "spring-cloud-aws"
    classifier = "schema"
    description = "Builds -${classifier} archive containing all " +
            "XSDs for deployment at http://www.elasticspring.org/schema/."

    subprojects.each { subproject ->
        def Properties schemas = new Properties();

        subproject.sourceSets.main.resources.find {
            it.path.endsWith("META-INF/spring.schemas")
        }?.withInputStream { schemas.load(it) }

        for (def key : schemas.keySet()) {
            def shortName = key.replaceAll(/http.*schema.(.*).elasticspring-.*/, '$1')
            assert shortName != key
            File xsdFile = subproject.sourceSets.main.resources.find {
                it.path.endsWith(schemas.get(key))
            } as File
            assert xsdFile != null
            into(shortName) {
                from xsdFile.path
            }
        }
    }
}

task distZip(type: Zip, dependsOn: [docsZip, schemaZip]) {
    group = "Distribution"
    baseName = "spring-cloud-aws"
    classifier = "dist"
    description = "Builds -${classifier} archive, containing all jars and docs, " +
            "suitable for community download page."

    ext.baseDir = "${baseName}-${project.version}";

    from(zipTree(docsZip.archivePath)) {
        into "${baseDir}/docs"
    }

    from(zipTree(schemaZip.archivePath)) {
        into "${baseDir}/schema"
    }

    subprojects.each { subproject ->
        into("${baseDir}/libs") {
            from subproject.jar
            if (subproject.tasks.findByPath("sourcesJar")) {
                from subproject.sourcesJar
            }
            if (subproject.tasks.findByPath("javadocJar")) {
                from subproject.javadocJar
            }
        }
    }
}

artifacts {
    archives docsZip
    archives schemaZip
    archives distZip
}


asciidoctor {
    options = [
            eruby     : 'erubis',
            attributes: [
                    'source-highlighter': 'coderay',
                    toc                 : 'right',
                    idprefix            : '',
                    idseparator         : '-'
            ]
    ]
}


task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts'
    gradleVersion = '1.11'
}
